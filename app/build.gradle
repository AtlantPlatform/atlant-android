apply plugin: 'com.android.application'
apply plugin: 'com.getkeepsafe.dexcount'

android {
    compileSdkVersion 27
    buildToolsVersion "26.0.2"
    defaultConfig {
        def release = (project.gradle.startParameter.taskNames.any { it.toLowerCase().contains('release') })
        applicationId "com.frostchein.atlant"
        minSdkVersion 16
        targetSdkVersion 27
        versionCode 10
        versionName "0.6"
        multiDexEnabled release
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        checkReleaseBuilds false
    }
    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def abi = output.getFilter(com.android.build.OutputFile.ABI)
            if (abi == null) {
                abi = "universal"
            }
            def nameBuild = variant.name
            def versionBuild = variant.versionName
            outputFileName = "atlant-" + nameBuild + "-" + abi + "-v" + versionBuild + ".apk"
        }
    }
    splits {
        abi {
            enable true
            reset()
            include 'x86'
            universalApk true
        }
    }
    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }
    packagingOptions {
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/notice'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license'
        exclude 'META-INF/license.txt'
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testCompile 'junit:junit:4.12'
    //Design
    compile 'com.android.support:appcompat-v7:27.0.1'
    compile 'com.android.support:recyclerview-v7:27.0.1'
    compile 'com.android.support:cardview-v7:27.0.1'
    compile 'com.android.support:design:27.0.1'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'net.danlew:android.joda:2.9.4.1'
    compile('com.mikepenz:materialdrawer:5.9.2@aar') {
        transitive = true
    }
    compile 'me.zhanghai.android.materialprogressbar:library:1.4.1'
    //Architecture
    compile 'com.android.support:multidex:1.0.2'
    compile 'com.jakewharton:butterknife:8.2.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.2.1'
    compile 'org.greenrobot:eventbus:3.0.0'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.8'
    compile 'com.google.dagger:dagger:2.8'
    //Network
    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    compile 'com.squareup.okhttp3:okhttp:3.8.1'
    compile 'com.google.code.gson:gson:2.8.0'
    compile 'com.squareup.retrofit2:converter-gson:2.1.0'
    //QR
    compile 'com.google.zxing:core:3.3.0'
    compile 'me.dm7.barcodescanner:zxing:1.9'
    //Ethereum
    compile 'org.web3j:core-android:2.2.1'
}
