apply plugin: 'com.android.application'

android {

    def scheme = '"https://"'
    def urlDev = "api-ropsten.etherscan.io/"
    def urlProd = "api.etherscan.io/"


    compileSdkVersion 30
    defaultConfig {
        vectorDrawables.useSupportLibrary = true
        applicationId "io.atlant.wallet"
        minSdkVersion 16
        targetSdkVersion 30
        versionCode 14
        versionName "0.6.4"
        multiDexEnabled true
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        checkReleaseBuilds false
    }

    flavorDimensions "default"
    productFlavors {
        dev {
            applicationId = "io.atlant.wallet.dev"
            buildConfigField 'String', 'BASE_URL', scheme + '+"' + urlDev + '"'
        }

        prod {
            applicationId = "io.atlant.wallet"
            buildConfigField 'String', 'BASE_URL', scheme + '+"' + urlProd + '"'
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def abi = output.getFilter(com.android.build.OutputFile.ABI)
            if (abi == null) {
                abi = "universal"
            }
            def nameBuild = variant.name
            def versionBuild = variant.versionName
            outputFileName = "atlant-wallet-" + nameBuild + "-" + abi + "-v" + versionBuild + ".apk"
        }
    }
    splits {
        abi {
            enable true
            reset()
            include 'x86'
            universalApk true
        }
    }
    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }
    packagingOptions {
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/notice'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license'
        exclude 'META-INF/license.txt'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    androidTestImplementation('androidx.test.espresso:espresso-core:3.1.0', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testImplementation 'junit:junit:4.13'
    //Design
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'com.squareup.picasso:picasso:2.5.2'
    implementation 'net.danlew:android.joda:2.9.4.1'
    implementation('com.mikepenz:materialdrawer:5.9.2@aar') {
        transitive = true
    }
    implementation 'me.zhanghai.android.materialprogressbar:library:1.4.1'
    //Architecture
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'com.jakewharton:butterknife:8.2.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.2.1'
    implementation 'org.greenrobot:eventbus:3.0.0'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.15'
    implementation 'com.google.dagger:dagger:2.27'
    //Network
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.okhttp3:okhttp:4.7.2'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'com.squareup.retrofit2:converter-gson:2.6.2'
    implementation 'com.facebook.stetho:stetho-okhttp3:1.5.1'
    //QR
    implementation 'com.google.zxing:core:3.3.3'
    implementation 'me.dm7.barcodescanner:zxing:1.9'
    //Ethereum
    implementation 'org.web3j:core-android:2.2.1'
}
